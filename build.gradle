plugins {
    id 'java'
	id 'application'
	id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'org.openjfx.javafxplugin' version '0.0.10'
	id 'de.jjohannes.extra-java-module-info' version '0.5'
}

repositories {
    mavenCentral()
    maven {
        name "Fabric"
        url 'https://maven.fabricmc.net/'
    }
}

archivesBaseName = 'matcher'
group = 'net.fabricmc'

sourceSets {
    main {
        java.srcDirs = ['src']
		resources.srcDirs = ['res']
    }
}

javafx {
	version = "11.0.2"
	modules = [ 'javafx.controls', 'javafx.web' ]
}

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

dependencies {
    implementation "com.github.javaparser:javaparser-core:3.24.0"
	implementation "net.fabricmc:procyon-fabric-compilertools:0.5.35.13"
	implementation "org.ow2.asm:asm:9.2"
	implementation "org.ow2.asm:asm-commons:9.2"
	implementation "org.ow2.asm:asm-tree:9.2"
	implementation "org.ow2.asm:asm-util:9.2"
	implementation "net.fabricmc:mapping-io:0.3.0"
	implementation "net.fabricmc:cfr:0.0.9"
	implementation "net.fabricmc:fabric-fernflower:1.4.1"
}

extraJavaModuleInfo {
	automaticModule("cfr-0.0.9.jar", "cfr")
	automaticModule("procyon-fabric-compilertools-0.5.35.13.jar", "procyon.compilertools")
	automaticModule("procyon-fabric-core-0.5.35.13.jar", "procyon.core")
	automaticModule("fabric-fernflower-1.4.1.jar", "intellij.fernflower")
}

application {
	mainClass = 'matcher.Main'
}

build.dependsOn(shadowJar)

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}